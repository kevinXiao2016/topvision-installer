; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2A15CB65-50D2-4135-A1C9-9BE604D47859}
AppName=@AppName@
AppVerName=@AppName@@AppVerName@
AppPublisher=Victor
AppPublisherURL=@CorpUrl@
AppSupportURL=@CorpUrl@
AppUpdatesURL=@CorpUrl@
AppCopyright={cm:MyAppCopyright}
DefaultDirName={reg:HKCU\Software\@CorpName@\@AppName@,InstallPath|{pf64}\@CorpName@}
DefaultGroupName=@CorpName@
OutputBaseFilename=@AppName@-Setup
LicenseFile=license.rtf
Compression=lzma
SolidCompression=yes
WindowVisible=yes
VersionInfoVersion=20
VersionInfoCompany=@CorpUrl@
VersionInfoDescription=@VersionInfoDescription@
WizardImageFile=wizard.bmp
ArchitecturesInstallIn64BitMode=x64 ia64

[Languages]
Name: "cn"; MessagesFile: "compiler:Languages/Chinese.isl"; LicenseFile: "license.rtf"
Name: "en"; MessagesFile: "compiler:Default.isl"; LicenseFile: "license_en.rtf"

[LangOptions]
cn.LanguageName=简体中文
en.LanguageName=English

[Messages]
cn.BeveledLabel=安装@AppName@@AppVerName@
en.BeveledLabel=@AppName@@AppVerName@ Setup

[CustomMessages]
cn.MyAppCopyright=Copyright(C)2010-2015,鼎点视讯科技有限公司版权所有
en.MyAppCopyright=Copyright(C)2010-2015,TOPVISION TECHNOLOGIES CO.,LTD. All rights reserved.
cn.FullInstall=完全安装
en.FullInstall=Full Install
cn.EponInstall=仅支持EPON安装
en.EponInstall=Epon install only
cn.CcmtsInstall=支持EPON和鼎点CMTS安装
en.CcmtsInstall=Epon&Cmts of Topvision install only
cn.CmtsInstall=支持其他厂商CMTS安装
en.CmtsInstall=Cmts of others install only
cn.WlanInstall=仅支持无线安装
en.WlanInstall=Wlan install only
cn.EocInstall=支持EOC和EPON安装
en.EocInstall=Epon&Eoc install only
cn.ApInstall=仅支持Ap安装
en.ApInstall=AP install only
cn.CustomInstall=自定义安装
en.CustomInstall=Custom install
cn.MainDesc=系统框架文件
en.MainDesc=Framework files
cn.ProductDesc=各个产品文件
en.ProductDesc=Product's files
cn.EponSupport=EPON管理所需要文件
en.EponSupport=Files of Epon support
cn.CcmtsSupport=CCMTS管理所需要文件
en.CcmtsSupport=Files of Ccmts support
cn.CmtsSupport=CMTS管理所需要文件
en.CmtsSupport=Files of Cmts support
cn.WlanSupport=无线管理所需要文件
en.WlanSupport=Files of Wlan support
cn.EocSupport=EOC管理所需要文件
en.EocSupport=Files of Eoc support
cn.ApSupport=AP管理所需要文件
en.ApSupport=Files of AP support
cn.StartService=启动@AppName@服务
en.StartService=Start @AppName@ Service
cn.StopService=停止@AppName@服务
en.StopService=Stop @AppName@ Service
cn.InstallService=安装@AppName@服务
en.InstallService=Install @AppName@ Service
cn.UninstallService=卸载@AppName@服务
en.UninstallService=Uninstall @AppName@ Service
cn.UninstallAppTitle=卸载
en.UninstallAppTitle=Uninstall
cn.AppClient=@AppName@客户端
en.AppClient=@AppName@ Client
cn.ToolsTitle=工具
en.ToolsTitle=Tools
cn.ProgramExisted=系统中已经安装了程序版本%1，是否卸载原来的版本后进行安装？
en.ProgramExisted=This program of %1 was installed in this system, Are you sure to uninstall old version and continue?
cn.UninstallError=卸载错误，请手动卸载后进行安装。
en.UninstallError=Uninstall fail，Please uninstall yourself then install again。
cn.MissingWOW64APIs=您正在运行的Windows版本不具备执行64位安装的功能。要解决此问题，请在64位操作系统执行此安装。
en.MissingWOW64APIs=The version of Windows you are running does not include functionality required by Setup to perform a 64-bit installation. To correct this problem, please install in a 64-bit windows.
cn.MustAdminLoggedOn=您必须是管理员登录或者以管理员身份运行此安装程序。
en.MustAdminLoggedOn=You must be an administrator to log in or run the installation as an administrator.
cn.NM3000Install=独立安装-@AppName@
en.NM3000Install=@AppName@ Full
cn.MysqlInstall=分布式安装-Mysql数据库
en.MysqlInstall=Mysql Only
cn.ServerInstall=分布式安装-Web服务器
en.ServerInstall=Web server only
cn.EngineMgrInstall=分布式安装-采集管理器
en.EngineMgrInstall=Engine manager only
cn.CustomInstall=自定义安装
en.CustomInstall=Custom
cn.ServerFiles=网管Server文件
en.ServerFiles=Server Files
cn.MysqlFiles=Mysql数据库文件
en.MysqlFiles=Mysql Files
cn.JreFiles=JRE运行环境
en.JreFiles=JRE Files
cn.EngineMgrFiles=采集管理器文件
en.EngineMgrFiles=Engine Manager Files
cn.StartMysql=启动Mysql服务
en.StartMysql=Start Mysql
cn.StopMysql=停止Mysql服务
en.StopMysql=Stop Mysql
cn.StartEngineMgr=启动采集管理器服务
en.StartEngineMgr=Start Engine Manager
cn.StopEngineMgr=停止采集管理器服务
en.StopEngineMgr=Stop Engine Manager
cn.InstallMysql=安装Mysql服务
en.InstallMysql=Install Mysql Service
cn.UninstallMysql=卸载Mysql服务
en.UninstallMysql=Uninstall Mysql Service
cn.InstallEngineMgr=安装采集管理器服务
en.InstallEngineMgr=Install Engine Manager Service
cn.UninstallEngineMgr=卸载采集管理器服务
en.UninstallEngineMgr=Uninstall Engine Manager Service
cn.engineCreate=创建采集器程序
en.engineCreate=Create Engine Program
cn.SelectIpAddressTitle=服务器地址配置
en.SelectIpAddressTitle=Server address configuration
cn.SelectIpAddressDesc=配置网管服务器地址，多网卡或者多IP时，请选择与被管理设备能够联通的地址
en.SelectIpAddressDesc=When configuring the network management server address, multiple network card or multiple IP, please select the address that can be connected with the managed device.
cn.SelectIpAddressCaption=请选择NM3000使用的IP地址
en.SelectIpAddressCaption=Please select the NM3000 server to use the IP address

[Tasks]
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Types]
Name: "full"; Description: "{cm:NM3000Install}"
Name: "serveronly"; Description: "{cm:ServerInstall}";
Name: "mysql"; Description: "{cm:MysqlInstall}"
Name: "enginemgr"; Description: "{cm:EngineMgrInstall}"
Name: "custom"; Description: "{cm:CustomInstall}"; Flags: iscustom

[Components]
Name: "server"; Description: "{cm:ServerFiles}"; Types: full serveronly
Name: "mysql"; Description: "{cm:MysqlFiles}"; Types: full mysql
Name: "jre"; Description: "{cm:JreFiles}"; Types: full serveronly enginemgr
Name: "enginemgr"; Description: "{cm:EngineMgrFiles}"; Types: enginemgr

[Files]
Source: "setup_background.bmp"; DestDir: "{tmp}"
Source: "setup_background_en.bmp"; DestDir: "{tmp}"
Source: "UpdateMem.exe"; DestDir: "{tmp}"

; 主文件 
Source: "..\dist\ems\mysql\*"; DestDir: "{app}\mysql"; Components: mysql; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\jre\*"; DestDir: "{app}\jre"; Components: jre; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\enginemgr\*"; DestDir: "{app}\enginemgr"; Components: enginemgr; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\*"; Excludes: "mysql,jre,\bin\*.bat,\bin\*.sh,\bin\nm3000Service*.exe,\monitor\*.bat,\webapp\WEB-INF\conf\config*.properties"; DestDir: "{app}"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Service-@OSArch@.exe"; DestName: "nm3000Service.exe"; DestDir: "{app}\bin\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; 选择语言默认配置文件
; config.properties
Source: "..\dist\ems\webapp\WEB-INF\conf\config.properties"; DestName: "config.properties"; DestDir: "{app}\webapp\WEB-INF\conf\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\webapp\WEB-INF\conf\config_en.properties"; DestName: "config.properties"; DestDir: "{app}\webapp\WEB-INF\conf\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs

; bin\*bat
Source: "..\dist\ems\bin\engineCreator.bat"; DestDir: "{app}\bin\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\runEngine.bat"; DestDir: "{app}\bin\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\runEngine.sh"; DestDir: "{app}\bin\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\update.bat"; DestDir: "{app}\bin\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist
Source: "..\dist\ems\bin\updateOptions.bat"; DestDir: "{app}\bin\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist

Source: "..\dist\ems\bin\nm3000Mgr.bat"; DestName: "nm3000Mgr.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Install-@OSArch@.bat"; DestName: "nm3000Install.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Run.bat"; DestName: "nm3000Run.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Restart.bat"; DestName: "nm3000Restart.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Start.bat"; DestName: "nm3000Start.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Stop.bat"; DestName: "nm3000Stop.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Uninstall.bat"; DestName: "nm3000Uninstall.bat"; DestDir: "{app}\bin\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs

Source: "..\dist\ems\bin\nm3000Mgr_en.bat"; DestName: "nm3000Mgr.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Install-@OSArch@_en.bat"; DestName: "nm3000Install.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Run.bat"; DestName: "nm3000Run.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Restart_en.bat"; DestName: "nm3000Restart.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Start_en.bat"; DestName: "nm3000Start.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Stop_en.bat"; DestName: "nm3000Stop.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\bin\nm3000Uninstall_en.bat"; DestName: "nm3000Uninstall.bat"; DestDir: "{app}\bin\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
; bin\*.exe
Source: "..\dist\ems\bin\diskspace.exe"; DestDir: "{app}\bin\"; Flags: onlyifdoesntexist
; monitor\*bat
Source: "..\dist\ems\monitor\monitor.bat"; DestName: "monitor.bat"; DestDir: "{app}\monitor\"; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorInstall.bat"; DestName: "monitorInstall.bat"; DestDir: "{app}\monitor\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorRun.bat"; DestName: "monitorRun.bat"; DestDir: "{app}\monitor\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorStart.bat"; DestName: "monitorStart.bat"; DestDir: "{app}\monitor\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorStop.bat"; DestName: "monitorStop.bat"; DestDir: "{app}\monitor\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorUninstall.bat"; DestName: "monitorUninstall.bat"; DestDir: "{app}\monitor\"; Languages: cn; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs

Source: "..\dist\ems\monitor\monitor.bat"; DestName: "monitor.bat"; DestDir: "{app}\monitor\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorInstall_en.bat"; DestName: "monitorInstall.bat"; DestDir: "{app}\monitor\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorRun.bat"; DestName: "monitorRun.bat"; DestDir: "{app}\monitor\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorStart_en.bat"; DestName: "monitorStart.bat"; DestDir: "{app}\monitor\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorStop_en.bat"; DestName: "monitorStop.bat"; DestDir: "{app}\monitor\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs
Source: "..\dist\ems\monitor\monitorUninstall_en.bat"; DestName: "monitorUninstall.bat"; DestDir: "{app}\monitor\"; Languages: en; Components: server; Flags: overwritereadonly skipifsourcedoesntexist ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\{cm:StartService}"; Filename: "{app}\bin\nm3000Start.bat"; WorkingDir: "{app}"; Components: server
Name: "{group}\{cm:StopService}"; Filename: "{app}\bin\nm3000Stop.bat"; WorkingDir: "{app}"; Components: server
Name: "{group}\{cm:StartMysql}"; Filename: "{app}\mysql\bin\startMysql.bat"; WorkingDir: "{app}"; Components: mysql
Name: "{group}\{cm:StopMysql}"; Filename: "{app}\mysql\bin\stopMysql.bat"; WorkingDir: "{app}"; Components: mysql
Name: "{group}\{cm:StartEngineMgr}"; Filename: "{app}\enginemgr\bin\engineMgrStart.bat"; WorkingDir: "{app}"; Components: enginemgr
Name: "{group}\{cm:StopEngineMgr}"; Filename: "{app}\enginemgr\bin\engineMgrStop.bat"; WorkingDir: "{app}"; Components: enginemgr
Name: "{group}\{cm:AppClient}"; Filename: "http://127.0.0.1:3000/"; Components: server
Name: "{group}\{cm:ToolsTitle}\{cm:InstallService}"; Filename: "{app}\bin\nm3000Install.bat"; WorkingDir: "{app}"; Components: server
Name: "{group}\{cm:ToolsTitle}\{cm:UninstallService}"; Filename: "{app}\bin\nm3000Uninstall.bat"; WorkingDir: "{app}"; Components: server
Name: "{group}\{cm:ToolsTitle}\{cm:InstallMysql}"; Filename: "{app}\mysql\bin\install.bat"; WorkingDir: "{app}"; Components: mysql
Name: "{group}\{cm:ToolsTitle}\{cm:UninstallMysql}"; Filename: "{app}\mysql\bin\remove.bat"; WorkingDir: "{app}"; Components: mysql
Name: "{group}\{cm:ToolsTitle}\{cm:InstallEngineMgr}"; Filename: "{app}\enginemgr\bin\engineMgrInstall.bat"; WorkingDir: "{app}"; Components: enginemgr
Name: "{group}\{cm:ToolsTitle}\{cm:UninstallEngineMgr}"; Filename: "{app}\enginemgr\bin\engineMgrUninstall.bat"; WorkingDir: "{app}"; Components: enginemgr
Name: "{group}\{cm:ToolsTitle}\{cm:UninstallAppTitle}"; Filename: "{uninstallexe}"; WorkingDir: "{app}"
Name: "{commondesktop}\{cm:StartService}"; Filename: "{app}\bin\nm3000Start.bat"; Tasks: desktopicon; WorkingDir: "{app}"; Components: server
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{cm:StartService}"; Filename: "{app}\bin\nm3000Start.bat"; Tasks: quicklaunchicon; WorkingDir: "{app}"; Components: server

[Registry]
Root: HKLM; Subkey: "Software\Microsoft\Internet Explorer\Main\FeatureControl\FEATURE_DISABLE_TELNET_PROTOCOL"; ValueType: dword; ValueName: "iexplore.exe"; ValueData: "00000000"; Components: server; Flags: uninsdeletevalue
Root: HKCU; Subkey: "Software\@CorpName@"; Components: server; Flags: uninsdeletekeyifempty
Root: HKCU; Subkey: "Software\@CorpName@\@AppName@"; ValueType: string; ValueName: "InstallPath"; ValueData: "{app}"; Components: server; Flags: deletevalue
Root: HKCU; Subkey: "Software\@CorpName@\@AppName@"; ValueType: string; ValueName: "Version"; ValueData: "@AppVerName@-D@AppVerDate@"; Components: server; Flags: deletevalue
Root: HKCU; Subkey: "Software\@CorpName@\@AppName@"; ValueType: string; ValueName: "LastVersion"; ValueData: "@AppVerName@"; Components: server; Flags: deletevalue
Root: HKCU; Subkey: "Software\@CorpName@\@AppName@"; ValueType: string; ValueName: "Status"; ValueData: "Installed"; Components: server; Flags: deletevalue
Root: HKCU; Subkey: "Software\@CorpName@\@AppName@"; ValueType: string; ValueName: "ServiceType"; ValueData: "Daemon"; Components: server; Flags: deletevalue
Root: HKCU; Subkey: "Software\@CorpName@\@AppName@"; ValueType: string; ValueName: "Language"; ValueData: "@LanguageName@"; Components: server; Flags: deletevalue

[Run]
; Filename: "{app}\bin\updateOptions.bat"; Parameters: "x64 hasServer hasMysql"; Description: "{cm:engineCreate}"; Components: server and mysql; Flags: skipifsilent
; Filename: "{app}\bin\updateOptions.bat"; Parameters: "x64 hasServer noMysql"; Description: "{cm:engineCreate}"; Components: server and (not mysql); Flags: skipifsilent
Filename: "{tmp}\UpdateMem.exe"; Parameters: "{app}\bin\nm3000Install.bat {app}\mysql\my.cnf hasServer hasMysql"; Description: "Update Memory"; Components: server and mysql; Flags: skipifsilent
Filename: "{tmp}\UpdateMem.exe"; Parameters: "{app}\bin\nm3000Install.bat {app}\mysql\my.cnf hasServer noMysql"; Description: "Update Memory"; Components: server and (not mysql); Flags: skipifsilent
Filename: "{tmp}\UpdateMem.exe"; Parameters: "{app}\bin\nm3000Install.bat {app}\mysql\my.cnf noServer hasMysql"; Description: "Update Memory"; Components: (not server) and mysql; Flags: skipifsilent
Filename: "{app}\bin\engineCreator.bat"; Description: "{cm:engineCreate}"; Components: server; Flags: skipifsilent
Filename: "{app}\bin\nm3000Install.bat"; Description: "{cm:InstallService}"; Components: server; Flags: skipifsilent
Filename: "{app}\mysql\bin\install.bat"; Description: "{cm:InstallMysql}"; Components: mysql; Flags: skipifsilent
Filename: "{app}\enginemgr\bin\engineMgrInstall.bat"; Description: "{cm:InstallEngineMgr}"; Components: enginemgr; Flags: skipifsilent
Filename: "{app}\bin\nm3000Start.bat"; Description: "{cm:StartService}"; Components: server; Flags: nowait postinstall skipifsilent
Filename: "{app}\enginemgr\bin\engineMgrStart.bat"; Description: "{cm:StartEngineMgr}"; Components: enginemgr; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

[UninstallRun]
Filename: "{app}\bin\nm3000Uninstall.bat"; Components: server
Filename: "{app}\mysql\bin\remove.bat"; Components: mysql
Filename: "{app}\enginemgr\bin\engineMgrUninstall.bat"; Components: enginemgr

[code]
const
    ERROR_INSUFFICIENT_BUFFER = 122;
var
    IpAddressPage: TwizardPage;
    IpAddressBox: TNewComboBox;

function GetIpAddrTable( pIpAddrTable: Array of Byte;
    var pdwSize: Cardinal; bOrder: WordBool ): DWORD;
external 'GetIpAddrTable@IpHlpApi.dll stdcall';

procedure GetIpAddresses(Addresses : TStringList);
var
    Size : Cardinal;
    Buffer : Array of Byte;
    IpAddr : String;
    AddrCount : Integer;
    I, J : Integer;
begin
    // Find Size
    if GetIpAddrTable(Buffer,Size,False) = ERROR_INSUFFICIENT_BUFFER then
    begin
        // Allocate Buffer with large enough size
        SetLength(Buffer,Size);
        // Get List of IP Addresses into Buffer
        if GetIpAddrTable(Buffer,Size,True) = 0 then
        begin
            // Find out how many addresses will be returned.
            AddrCount := (Buffer[1] * 256) + Buffer[0];
            // Loop through addresses.
            For I := 0 to AddrCount -1 do
            begin
                IpAddr := '';
                // Loop through each byte of the address
                For J := 0 to 3 do
                begin
                    if J > 0 then
                        IpAddr := IpAddr + '.';
                    // Navigagte through record structure to find correct byte of Addr
                    IpAddr := IpAddr + IntToStr(Buffer[I*24+J+4]);
                end;
                if IpAddr <> '127.0.0.1' then
                    Addresses.Add(IpAddr);
            end;
        end;
    end;
end;

procedure InitializeWizard();
var
    addrs : TStringList;
    lbl: TLabel;
begin
    addrs := TStringList.Create;
    GetIpAddresses(addrs);
    IpAddressPage:= CreateCustomPage(wpSelectDir, CustomMessage('SelectIpAddressTitle'), CustomMessage('SelectIpAddressDesc'));
    lbl := TLabel.Create(IpAddressPage);
    lbl.Parent := IpAddressPage.Surface;
    lbl.Caption := CustomMessage('SelectIpAddressCaption');
    IpAddressBox := TNewComboBox.Create(IpAddressPage);
    IpAddressBox.Parent := IpAddressPage.Surface;
    IpAddressBox.Width := ScaleX(250);
    IpAddressBox.Top := lbl.Top + 30;
    IpAddressBox.Style := csDropDownList;
    IpAddressBox.Items.AddStrings(addrs);
    IpAddressBox.ItemIndex :=0;
    addrs.Free;
end;

//判断是否已经安装过
function InitializeSetup(): Boolean;
var
    sStatus, sMsg, sVersion, sPath: String;
    ErrorCode: Integer;
begin
    Result := True;
    //判断是否是64位
    if IsWin64() = False then begin
        //SetupMessage(msgMissingWOW64APIs)
        MsgBox(CustomMessage('MissingWOW64APIs'), mbConfirmation, MB_OK);
        Result := False;
        Exit;
    end;
    //判断是否是管理员运行的
    if IsAdminLoggedOn() = False then begin
        MsgBox(CustomMessage('MustAdminLoggedOn'), mbConfirmation, MB_OK);
        Result := False;
        Exit;
    end;
    //判断注册表中卸载文件路径是否存在
    if RegQueryStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'UninstallPath', sPath) then begin
        if(FileExists(sPath)) then begin
            RegQueryStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'LastVersion', sVersion);
            sMsg := FmtMessage(CustomMessage('ProgramExisted'), [sVersion]);
            if MsgBox(sMsg, mbConfirmation, MB_YESNO) = IDYES then begin
                if(Exec(sPath, '', '', SW_SHOWNORMAL, ewWaitUntilTerminated, ErrorCode) = False) then begin
                    MsgBox(CustomMessage('UninstallError'), mbConfirmation, MB_OK);
                    Result := False;
                end;
                if(FileExists(sPath)) then begin
                    Sleep(1000);
                end;
                if(FileExists(sPath)) then begin
                    Sleep(2000);
                end;
                if(FileExists(sPath)) then begin
                    Sleep(3000);
                end;
                if(FileExists(sPath)) then begin
                    MsgBox(CustomMessage('UninstallError'), mbConfirmation, MB_OK);
                    Result := False;
                end;
            end else begin
                Result:= False;
            end;
         end;
     end;
end;
//安装结束后写注册表
procedure CurStepChanged(CurStep: TSetupStep);
var
    uninspath, uninsname, sDate, sFilename, sHasServer, sHasMysql: string;
    ErrorCode: Integer;
begin
    if CurStep=ssDone then begin
        uninspath:= ExtractFilePath(ExpandConstant('{uninstallexe}'));
        uninsname:= Copy(ExtractFileName(ExpandConstant('{uninstallexe}')),1,8);
        RegWriteStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'UninstallPath', uninspath + uninsname + '.exe');
        
        sDate := GetDateTimeString('yyyy/mm/dd hh:nn:ss', '-', ':');
        RegWriteStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'InstallTime', sDate);
        RegWriteStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'Language', ActiveLanguage);
        if IpAddressBox.Text <> '' then
        begin
            SetIniString('RMI','java.rmi.server.hostname',IpAddressBox.Text,ExpandConstant('{app}/webapp/WEB-INF/conf/config.properties'));
            if(FileExists(ExpandConstant('{app}/enginemgr/conf/engine.properties'))) then
                SetIniString('RMI','java.rmi.server.hostname',IpAddressBox.Text,ExpandConstant('{app}/enginemgr/conf/engine.properties'));
        end;
    end;
end;
//卸载结束后写注册表
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
    sDate: string;
begin
    if CurUninstallStep = usDone then begin
        sDate := GetDateTimeString('yyyy/mm/dd hh:nn:ss', '-', ':');
        RegWriteStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'UninstallTime', sDate);
        RegWriteStringValue(HKEY_CURRENT_USER, 'Software\@CorpName@\@AppName@', 'Status', 'Uninstalled');
    end;
end;